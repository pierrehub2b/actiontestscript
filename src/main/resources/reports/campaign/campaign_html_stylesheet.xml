<?xml version="1.0" encoding="UTF-8" ?>
<xsl:stylesheet version="1.0"
	xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:java="http://xml.apache.org/xslt/java"
	exclude-result-prefixes="java">
	<xsl:output method="html" version="5.0" encoding="UTF-8"
		indent="yes" />
	<!-- ========================= -->
	<!-- Calculations -->
	<!-- ========================= -->

	<xsl:variable name="picFail" select="//report/pics/pic[@name='fail']" />
	<xsl:variable name="picPass" select="//report/pics/pic[@name='pass']" />
		
	<xsl:variable name="xml_reports"
		select="//report/suite/parameter[@name='xml.report']/@value" />
	<xsl:variable name="atsv_reports"
		select="//report/suite/parameter[@name='visual.report']/@value" />
	<xsl:variable name="ats_loglevel"
		select="//report/suite/parameter[@name='ats.log.level']/@value" />
	<xsl:variable name="actions" select="//report/@actions" />
	<xsl:variable name="details" select="//report/@details" />

	<xsl:template name="substring-after-last">
		<xsl:param name="string" />
		<xsl:param name="delimiter" />
		<xsl:choose>
			<xsl:when test="contains($string, $delimiter)">
				<xsl:call-template name="substring-after-last">
					<xsl:with-param name="string"
						select="substring-after($string, $delimiter)" />
					<xsl:with-param name="delimiter" select="$delimiter" />
				</xsl:call-template>
			</xsl:when>
			<xsl:otherwise>
				<xsl:value-of select="$string" />
			</xsl:otherwise>
		</xsl:choose>
	</xsl:template>

	<xsl:template name="millisecs-to-ISO">
		<xsl:param name="millisecs" />

		<xsl:param name="JDN"
			select="floor($millisecs div 86400000) + 2440588" />
		<xsl:param name="mSec" select="$millisecs mod 86400000" />

		<xsl:param name="f"
			select="$JDN + 1401 + floor((floor((4 * $JDN + 274277) div 146097) * 3) div 4) - 38" />
		<xsl:param name="e" select="4*$f + 3" />
		<xsl:param name="g" select="floor(($e mod 1461) div 4)" />
		<xsl:param name="h" select="5*$g + 2" />

		<xsl:param name="d" select="floor(($h mod 153) div 5 ) + 1" />
		<xsl:param name="m"
			select="(floor($h div 153) + 2) mod 12 + 1" />
		<xsl:param name="y"
			select="floor($e div 1461) - 4716 + floor((14 - $m) div 12)" />

		<xsl:param name="H" select="floor($mSec div 3600000)" />
		<xsl:param name="M"
			select="floor($mSec mod 3600000 div 60000)" />
		<xsl:param name="S" select="$mSec mod 60000 div 1000" />

		<xsl:value-of
			select="concat($y, format-number($m, '-00'), format-number($d, '-00'))" />
		<xsl:value-of
			select="concat(format-number($H, 'T00'), format-number($M, ':00'), format-number($S, ':00'))" />
	</xsl:template>

	<!-- ========================= -->
	<!-- Appel du template: ats -->
	<!-- ========================= -->
	<xsl:template match="report">
		<HTML LANG="en">
			<HEAD>
				<META charset="utf-8" />
				<META name="description" content="" />
				<TITLE>Ats suites reporting</TITLE>
				<LINK rel="stylesheet" href="report.css" />

				<script type="text/javascript">
					function loopCollapsible(){
					var coll =
					document.getElementsByClassName("actionDetails");
					for (var i=0,
					item; item = coll[i]; i++) {
					coll[i].addEventListener("click",
					function() {
					var content = this.nextElementSibling;
					if
					(content.style.maxHeight){
					content.style.maxHeight = null;
					} else {
					content.style.maxHeight = content.scrollHeight + "px";
					}
					});
					}
					}
				</script>
				<style>
					HTML * {font-family: Arial}
					TABLE, TD {border:0px;margin-bottom:0.1rem;border-top:1px;border-collapse:collapse;border-spacing:0;}
					TABLE TD {border-top:thin solid;border-bottom:thin solid;border-right:thin solid;border-color:#ffffff}
					TD.titleClass {border-top:none;border-bottom:none;border-right:none;border-color:#000000}
					HR {height:2px;color:#696969;background-color:#696969; width:100%;border:none;margin-bottom:20px}
				</style>
			</HEAD>
			<BODY onload="loopCollapsible()">
				<TABLE>
					<TBODY>
						<TR>
							<TD class="titleClass">
								<IMG src="logo.png" />
							</TD>
							<TD class="titleClass">
								<span
									style="color: #565656; text-shadow: 2px 2px 8px #a5a5a5">
									<FONT SIZE="6">
										<B>
											ActionTestScript - Suites report
										</B>
									</FONT>
									<br />
									<FONT SIZE="3" COLOR="#565656">
										<I>
											<xsl:choose>
												<xsl:when test="$actions = 'true'">
													<xsl:choose>
														<xsl:when test="$details = 'true'">
															Full detailed report
														</xsl:when>
														<xsl:otherwise>
															Detailed report
														</xsl:otherwise>
													</xsl:choose>
												</xsl:when>
												<xsl:otherwise>
													Simplified report
												</xsl:otherwise>
											</xsl:choose>
											 - 
											<xsl:value-of select="count(suite)"/>
											suite(s) launched (created at
											<xsl:value-of
												select="java:format(java:java.text.SimpleDateFormat.new('yyyy.MM.dd-HH:mm'), java:java.util.Date.new())" />
											)
										</I>
									</FONT>
								</span>
							</TD>
						</TR>
					</TBODY>
				</TABLE>
				<DIV>
					<xsl:apply-templates />
				</DIV>
			</BODY>
		</HTML>
	</xsl:template>
	
	<xsl:template match="pic">
	</xsl:template>
		
	<!-- ========================= -->
	<!-- child element: suite -->
	<!-- ========================= -->
			
	<xsl:template match="suite">
	
		<xsl:variable name="suiteName" select="@name" />
		<xsl:variable name="countTests" select="count(tests/ats)" />
		<xsl:variable name="failedTests" select="count(tests/ats/script/summary[@status = '0'])" />
		<xsl:variable name="passedTests" select="count(tests/ats/script/summary[@status = '1'])" />

		<TABLE style="background-color:#cbcbcb" width="100%">
			<TBODY>
				<THEAD>
					<TR style="background-color:#787779;color:#ffffff">
						<TD colspan="3"
							style="padding: 0px 0px 0px 15px;border-style:solid;border-width: 0px 0px 0px 0px;">
							<FONT SIZE="5">
								<B>
									<xsl:value-of select="@name" />
								</B>
							</FONT>
							<i>
								-
								<xsl:value-of select="$countTests" />
								test(s) executed
							</i>
						</TD>
						<TD style="border-style:solid;border-width: 0px 0px 0px 0px;padding: 2px 5px 2px 0px;">
							<xsl:choose>
								<xsl:when test="$failedTests = 0">
									<IMG src="true.png" width="34" height="34" align="right"/>
								</xsl:when>
								<xsl:otherwise>
									<IMG src="false.png" width="34" height="34" align="right"/>
								</xsl:otherwise>
							</xsl:choose>
						</TD>
					</TR>
				</THEAD>
				<TR height="25">
					<TD COLSPAN="1" style="padding: 2px 0px 0px 15px;" width="80">
						<font size="2" color="#5d5d5d">
							<i>
								<b>Logs level</b>
							</i>
						</font>
					</TD>
					<TD COLSPAN="1" style="padding: 2px 0px 0px 15px;">
						<xsl:value-of select="$ats_loglevel" />
					</TD>
					<TD COLSPAN="2" style="text-align:right">
						<xsl:text disable-output-escaping="yes">&lt;A target=&quot;_blank&quot; href=&quot;</xsl:text>
						<xsl:value-of select="concat('ats-report.pdf')" />
						<xsl:text disable-output-escaping="yes">&quot; class=&quot;btn btn-secondary btn-sm &quot; role=&quot;button&quot; aria-pressed=&quot;true&quot;&gt;PDF report&lt;/a&gt;</xsl:text>
					</TD>
				</TR>
				<TR height="25">
					<TD colspan="1" style="padding: 0px 0px 0px 15px;" width="80">
						<font size="2" color="#5d5d5d">
							<i>
								<b>Result</b>
							</i>
						</font>
					</TD>
					<TD style="padding: 0px 0px 0px 15px;">
						<xsl:choose>
							<xsl:when test="$countTests = $passedTests">
								all tests passed !
							</xsl:when>
							<xsl:otherwise>
								<xsl:choose>
									<xsl:when test="$countTests = $failedTests">
										all tests failed !
									</xsl:when>
									<xsl:otherwise>
										<xsl:value-of select="$passedTests" />
										test(s) passed,
										<xsl:value-of select="$failedTests" />
										test(s) failed.
									</xsl:otherwise>
								</xsl:choose>
							</xsl:otherwise>
						</xsl:choose>
					</TD>
				</TR>
				<xsl:apply-templates select="test" />
			</TBODY>
		</TABLE>
		<HR/>
		<xsl:apply-templates select="tests">
			<xsl:with-param name="suiteName" select="$suiteName"/>
		</xsl:apply-templates>
	</xsl:template>

	<!-- ========================= -->
	<!-- child element: tests -->
	<!-- ========================= -->
	
	<xsl:template match="tests">
		<xsl:param name="suiteName"/>
		<div style="padding-left:6%">
			<xsl:apply-templates select="ats">
				<xsl:with-param name="suiteName" select="$suiteName"/>
			</xsl:apply-templates>
		</div>
	</xsl:template>
	
	<!-- ========================= -->
	<!-- child element: ats -->
	<!-- ========================= -->
	
	<xsl:template match="ats">
		<xsl:param name="suiteName"/>
		<xsl:variable name="testName" select="concat($suiteName, '/', script[1]/@testName)" />
		<div style="color: #565656; text-shadow: 2px 2px 8px #b9b9b9">
			<FONT SIZE="4">
				<B>
					<xsl:value-of select="script/@testName" />
				</B>
			</FONT>
			<FONT SIZE="2" COLOR="#565656">
				<I>
					(executed in
					<xsl:value-of select="sum(actions/action/duration)" />
					ms)
				</I>
			</FONT>
		</div>
		<TABLE border="1" bordercolor="#ffffff" cellpadding="10"
			width="100%" style="background-color:#C4C4C4">
			<THEAD>
				<TR style="background-color:#575757;color:white">
					<TH>
						Actions
					</TH>
					<TH>
						Started
					</TH>

					<TH>
						Author
					</TH>
					<TH>
						Links
					</TH>
					<TH>
						Result
					</TH>
				</TR>
			</THEAD>
			<TBODY>
				<TR>
					<TD style="text-align:center">
						<xsl:value-of select="script/summary/@actions" />
					</TD>
					<TD>
						<xsl:call-template name="millisecs-to-ISO">
							<xsl:with-param name="millisecs"
								select="script/started" />
						</xsl:call-template>
					</TD>
					<TD>
						<xsl:value-of select="script/author" />
					</TD>
					<TD class="linkButtons">
						<a>
							<xsl:attribute name="href">
							<xsl:value-of select="concat($testName,'.html')"/>
							</xsl:attribute>
							HTML report
						</a>
						<a>
							<xsl:attribute name="href">
							<xsl:value-of select="concat($testName,'.pdf')"/>
							</xsl:attribute>
							PDF report
						</a>
						<a>
							<xsl:attribute name="href">
							<xsl:value-of select="concat($testName,'.atsv')"/>
							</xsl:attribute>
							ATSV report
						</a>
					</TD>
					<xsl:choose>
						<xsl:when test="script/summary/@status = '0'">
							<TD class="test-failed" style="horizontal-align:right"
								width="50">
								<img align="right" alt="Test failed">
									<xsl:attribute name="src">
										<xsl:value-of select="$picFail"/>
									</xsl:attribute>
								</img>	
							</TD>
						</xsl:when>
						<xsl:when test="script/summary/@status = '1'">
							<TD class="test-passed" style="horizontal-align:right"
								width="50">
								<img align="right" alt="Test passed">
									<xsl:attribute name="src">
										<xsl:value-of select="$picPass"/>
									</xsl:attribute>
								</img>		
							</TD>
						</xsl:when>
					</xsl:choose>
				</TR>
				<xsl:choose>
					<xsl:when test="script/summary/data != '[empty]'">
						<TR>
							<TD colspan="1">
								<font size="2" color="#5d5d5d">
									<i>
										<b>Summary</b>
									</i>
								</font>
							</TD>
							<TD colspan="7">
								<xsl:value-of select="script/summary/data"
									disable-output-escaping="yes" />
							</TD>
						</TR>
					</xsl:when>
				</xsl:choose>
				<xsl:choose>
					<xsl:when test="script/summary/error != ''">
						<TR>
							<TD colspan="8">
								<font color="#b32e42">
									<b>
										Error in script
									</b>
								</font>
								<i>
									(
									<xsl:value-of
										select="script/summary/error/@script"
										disable-output-escaping="yes" />
									)
								</i>
								<xsl:value-of select="script/summary/error"
									disable-output-escaping="yes" />
							</TD>
						</TR>
					</xsl:when>
				</xsl:choose>
				<xsl:choose>
					<xsl:when test="$actions = 'true'">
						<xsl:choose>
							<xsl:when test="script/prerequisite != ''">
								<TR>
									<TD colspan="1">
										<font size="2" color="#5d5d5d">
											<i>
												<b>Prerequisite</b>
											</i>
										</font>
									</TD>
									<TD colspan="7">
										<xsl:value-of select="script/prerequisite"
											disable-output-escaping="yes" />
									</TD>
								</TR>
							</xsl:when>
						</xsl:choose>
						<xsl:choose>
							<xsl:when test="script/description != ''">
								<TR>
									<TD colspan="1">
										<font size="2" color="#5d5d5d">
											<i>
												<b>Description</b>
											</i>
										</font>
									</TD>
									<TD colspan="7">
										<xsl:value-of select="script/description"
											disable-output-escaping="yes" />
									</TD>
								</TR>
							</xsl:when>
						</xsl:choose>
					</xsl:when>
				</xsl:choose>
			</TBODY>
		</TABLE>
		<xsl:choose>
			<xsl:when test="$actions = 'true'">
				<xsl:apply-templates select="actions">
					<xsl:with-param name="fullTestName" select="$testName"/>
				</xsl:apply-templates>
			</xsl:when>
		</xsl:choose>
		<HR/>
	</xsl:template>

	<!-- ========================= -->
	<!-- child element: actions -->
	<!-- ========================= -->
	
	<xsl:template match="actions">
		<xsl:param name="fullTestName"/>
		<xsl:apply-templates select="action" >
			<xsl:with-param name="fullTestName" select="$fullTestName"/>
		</xsl:apply-templates>
	</xsl:template>

	<!-- ========================= -->
	<!-- child element: action -->
	<!-- ========================= -->
	<xsl:template match="action">
		<xsl:param name="fullTestName"/>
		<xsl:choose>
			<xsl:when test="$details = 'true'">
				<table width="100%" class="actionDetails"
					style="background-color:#787779;cursor: pointer" border="1"
					bordercolor="#ffffff">
					<tr height="36px">
						<td style="padding-left: 8px;color: #ffffff; border: 0">
							<xsl:call-template name="substring-after-last">
								<xsl:with-param name="string" select="@type" />
								<xsl:with-param name="delimiter" select="'.'" />
							</xsl:call-template>
						</td>
						<td width="100%"
							style="padding-left: 8px;color: #ffffff; border: 0">
							<i>
								<FONT SIZE="2">
									(executed in
									<xsl:value-of select="duration" />
									ms)
								</FONT>
							</i>
						</td>
						<td style="padding-right: 4px;border :0">
							<xsl:choose>
								<xsl:when test="passed = 'true'">
									<IMG src="true.png" class='actionStatusImg' height="30px"
										width="30px" style="vertical-align:middle" />
								</xsl:when>
								<xsl:when test="passed = 'warning'">
									<IMG src="warning.png" class='actionStatusImg'
										height="30px" width="30px" style="vertical-align:middle" />
								</xsl:when>
								<xsl:when test="passed = 'false'">
								<img align="right" alt="Test failed" height="30px" width="30px" style="vertical-align:middle">
									<xsl:attribute name="src">
										<xsl:value-of select="$picFail"/>
									</xsl:attribute>
								</img>	
								</xsl:when>
							</xsl:choose>
						</td>
					</tr>
				</table>
			</xsl:when>
			<xsl:otherwise>
				<table width="100%" style="background-color:#787779"
					border="1" bordercolor="#ffffff">
					<tr height="36px">
						<td style="padding-left: 8px;color: #ffffff; border: 0">
							<xsl:call-template name="substring-after-last">
								<xsl:with-param name="string" select="@type" />
								<xsl:with-param name="delimiter" select="'.'" />
							</xsl:call-template>
						</td>
						<td width="100%"
							style="padding-left: 8px;color: #ffffff; border: 0">
							<i>
								<FONT SIZE="2">
									(executed in
									<xsl:value-of select="duration" />
									ms)
								</FONT>
							</i>
						</td>
						<td style="padding-right: 4px;border :0">
							<xsl:choose>
								<xsl:when test="passed = 'true'">
									<IMG src="true.png" class='actionStatusImg' height="30px"
										width="30px" style="vertical-align:middle" />
								</xsl:when>
								<xsl:when test="passed = 'warning'">
									<IMG src="warning.png" class='actionStatusImg'
										height="30px" width="30px" style="vertical-align:middle" />
								</xsl:when>
								<xsl:when test="passed = 'false'">
									<IMG src="false.png" class='actionStatusImg' height="30px"
										width="30px" style="vertical-align:middle" />
								</xsl:when>
							</xsl:choose>
						</td>
					</tr>
				</table>
			</xsl:otherwise>
		</xsl:choose>
		<xsl:choose>
			<xsl:when test="$details = 'true'">
				<div class="content">
					<TABLE class="results table table-bordered"
						style="background-color:#EBEBEB">
						<THEAD>
							<TH class="w-75 p-3">
								Action :
								<xsl:value-of select="@type" />
							</TH>
						</THEAD>
						<TBODY>
							<TR>
								<TD class="w-100 p-3" COLSPAN="2">
									<xsl:variable name="img"
										select="concat($fullTestName ,'_xml/',img/@src)" />
									<IMG width="100%" SRC="{$img}" />
								</TD>
							</TR>
							<TR>
								<TD class="w-100 p-3" COLSPAN="2">
									Channel name :
									<xsl:value-of select="channel/@name" />
								</TD>
							</TR>
							<TR>
								<TD class="w-100 p-3" COLSPAN="2">
									Timeline :
									<xsl:value-of select="timeLine" />
								</TD>
							</TR>
							<xsl:choose>
								<xsl:when test="element/@tag != ''">
									<TR>
										<TD class="w-100 p-3" COLSPAN="2">
											Element tag :
											<xsl:value-of select="element/@tag" />
										</TD>
									</TR>
								</xsl:when>
							</xsl:choose>
							<xsl:choose>
								<xsl:when test="element/criterias != ''">
									<TR>
										<TD class="w-100 p-3" COLSPAN="2">
											Criterias :
											<xsl:value-of select="element/criterias" />
										</TD>
									</TR>
								</xsl:when>
							</xsl:choose>

							<xsl:choose>
								<xsl:when test="data != ''">
									<TR>
										<TD class="w-100 p-3" COLSPAN="2">
											Data :
											<xsl:value-of select="data" />
										</TD>
									</TR>
								</xsl:when>
							</xsl:choose>
						</TBODY>
					</TABLE>
				</div>
			</xsl:when>
		</xsl:choose>
	</xsl:template>
</xsl:stylesheet>